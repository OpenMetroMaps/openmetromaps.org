plugins {
    id 'com.craigburke.bower-installer' version '2.5.1'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'maven'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        mavenCentral()
    }
}

project(':openmetromaps-core') {
    apply plugin: 'java'
    apply plugin: 'eclipse-wtp'
}

project(':openmetromaps-dynamic') {
    apply plugin: 'war'
    apply plugin: 'eclipse-wtp'
}

project(':openmetromaps-core') {
    dependencies {
        compile 'de.topobyte:jsoup-elements:0.0.15'
        compile 'de.topobyte:jsoup-bootstrap:0.0.7'
        compile 'de.topobyte:webpaths-core:0.0.1'
        compile 'de.topobyte:pagegen-core:0.0.1'
        compile 'de.topobyte:pagegen-bootstrap:0.0.1'
        compile 'commons-io:commons-io:2.4'

        testCompile 'junit:junit:4.12'
    }
}

project(':openmetromaps-dynamic') {
    dependencies {
        compile project(':openmetromaps-core')

        compile 'de.topobyte:system-utils:0.0.1'
        compile 'de.topobyte:jsoup-servlet:0.0.1'
        compile 'de.topobyte:jsoup-utils:0.0.1'
        compile 'com.vladsch.flexmark:flexmark:0.22.20'
        compile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.22.20'
        compile 'com.vladsch.flexmark:flexmark-ext-tables:0.22.20'
        compile 'com.vladsch.flexmark:flexmark-ext-wikilink:0.22.20'

        providedCompile 'javax.servlet:javax.servlet-api:3.+'
        providedCompile 'javax.servlet.jsp:jsp-api:2.1'

        compile 'org.slf4j:slf4j-api:1.7.20'
        runtime 'org.slf4j:slf4j-log4j12:1.7.20'
        compile 'log4j:log4j:1.2.17'
    }
}

project(':openmetromaps-dynamic') {
    eclipse {
        wtp {
            component {
                contextPath = '/'
                deployName = 'openmetromaps'
                resource deployPath: '/', sourcePath: 'src/main/webapp'
                resource deployPath: '/', sourcePath: 'res'
                resource deployPath: '/', sourcePath: 'res_bower'
                resource deployPath: '/', sourcePath: 'build/favicons'
            }
        }
    }
    eclipse.classpath.file {
        withXml {
            def node = it.asNode()
            node.appendNode('classpathentry', [kind: 'src', path: 'logging/devel'])
            node.appendNode('classpathentry', [kind: 'src', path: 'config/devel'])
        }
        // Classpath entry for Eclipse which changes the order of classpathentries; otherwise no sources for 3rd party jars are shown
        withXml { xml ->
            def node = xml.asNode()
            node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
            node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
        }
    }

    bower {
        installBase = 'bower'

        'jquery'('2.2.2') {
            source 'dist/*.js' >> 'js/'
        }

        'bootstrap'('3.3.6') {
            source 'dist/css/*.css' >> 'styles/'
            source 'dist/fonts/**' >> 'fonts/'
            source 'dist/js/*.js' >> 'js/'
            source 'js/*.js' >> 'js/'
        }
    }

    def commonWarDefs = {
        from 'res'
        from 'res_bower'
        from 'build/favicons'
    }

    war {
        classifier = 'production'
        configure commonWarDefs
    }

    [war].each { task ->
        task.dependsOn bowerInstall
        task.from ('logging/' + task.classifier) {
            into('WEB-INF/classes')
            include "**/*"
        }
        task.from ('config/' + task.classifier) {
            into('WEB-INF/classes')
            include "**/*"
        }
    }

    task generateIcons {
        doLast {
            def svg = 'res/images/icon.svg'
            def dir = new File(buildDir, 'favicons')
            def subdir = new File(dir, 'images')
            dir.mkdirs()
            subdir.mkdirs()
            def out = 'favicon-%d.png'
            def sizes = [16, 32, 48, 64, 96]
            for (size in sizes) {
                def filename = String.format(out, size)
                def file = new File(subdir, filename)
                println 'Generate icon: ' + file
                exec {
                    commandLine = 'inkscape'
                    args = ['-C', '-h', size, '-e', file.toString(), svg]
                }
            }

            def icoFile = new File(dir, 'favicon.ico')
            println 'Generate icon: ' + icoFile

            def icoFiles = []
            def icoSizes = [16, 32, 48, 64]
            for (size in icoSizes) {
                def filename = String.format(out, size)
                def file = new File(subdir, filename)
                icoFiles << file.toString()
            }
            exec {
                commandLine = 'convert'
                args icoFiles
                args icoFile.toString()
            }
        }
    }

    war.dependsOn generateIcons
}
