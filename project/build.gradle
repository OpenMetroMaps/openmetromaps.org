buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url 'http://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'gradle.plugin.com.craigburke.gradle:client-dependencies:1.4.0'
        classpath('de.topobyte:gradle-generate-favicons-plugin:0.0.1');
    }
}

wrapper {
    gradleVersion = '5.2.1'
}

allprojects {
    group = pGroup
    version = pVersion
}

subprojects {
    apply plugin: 'maven'

    repositories {
        maven {
            url 'http://mvn.topobyte.de'
        }
        mavenCentral()
    }
}

project(':openmetromaps-core') {
    apply plugin: 'java'
    apply plugin: 'eclipse-wtp'
}

project(':openmetromaps-dynamic') {
    apply plugin: 'war'
    apply plugin: 'eclipse-wtp'
    apply plugin: 'com.craigburke.client-dependencies'
    apply plugin: 'de.topobyte.generate-favicons-gradle-plugin'
}

subprojects {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

project(':openmetromaps-core') {
    dependencies {
        compile 'de.topobyte:jsoup-elements:0.1.1'
        compile 'de.topobyte:jsoup-bootstrap:0.1.2'
        compile 'de.topobyte:jsoup-utils:0.1.3'
        compile 'de.topobyte:webpaths-core:0.0.2'
        compile 'de.topobyte:pagegen-core:0.1.0'
        compile 'de.topobyte:pagegen-bootstrap:0.1.1'
        compile 'commons-io:commons-io:2.4'

        testCompile 'junit:junit:4.12'
    }
}

project(':openmetromaps-dynamic') {
    dependencies {
        compile project(':openmetromaps-core')

        compile 'de.topobyte:system-utils:0.0.1'
        compile 'de.topobyte:jsoup-servlet:0.1.0'
        compile 'de.topobyte:jsoup-utils:0.1.3'
        compile 'com.vladsch.flexmark:flexmark:0.28.18'
        compile 'com.vladsch.flexmark:flexmark-ext-gfm-strikethrough:0.28.18'
        compile 'com.vladsch.flexmark:flexmark-ext-tables:0.28.18'
        compile 'com.vladsch.flexmark:flexmark-ext-wikilink:0.28.18'

        providedCompile 'javax.servlet:javax.servlet-api:3.+'
        providedCompile 'javax.servlet.jsp:jsp-api:2.1'

        compile 'org.slf4j:slf4j-api:1.7.20'
        compile 'ch.qos.logback:logback-classic:1.2.3'
    }
}

project(':openmetromaps-dynamic') {
    favicons {
        input = 'res/images/icon.svg'
    }
}

project(':openmetromaps-dynamic') {
    eclipse {
        wtp {
            component {
                contextPath = '/'
                deployName = 'openmetromaps'
                resource deployPath: '/', sourcePath: 'src/main/webapp'
                resource deployPath: '/', sourcePath: 'res'
                resource deployPath: '/client', sourcePath: 'src/assets/vendor'
                resource deployPath: '/', sourcePath: 'build/favicons'
            }
        }
    }
    eclipse.classpath.file {
        withXml {
            def node = it.asNode()
            node.appendNode('classpathentry', [kind: 'src', output: 'bin/main', path: 'logging/devel'])
            node.appendNode('classpathentry', [kind: 'src', output: 'bin/main', path: 'config/devel'])
        }
        // Classpath entry for Eclipse which changes the order of classpathentries; otherwise no sources for 3rd party jars are shown
        withXml { xml ->
            def node = xml.asNode()
            node.remove( node.find { it.@path == 'org.eclipse.jst.j2ee.internal.web.container' } )
            node.appendNode( 'classpathentry', [ kind: 'con', path: 'org.eclipse.jst.j2ee.internal.web.container', exported: 'true'])
        }
    }

    clientDependencies {
        copyExcludes = ['**/*.map', '**/Gruntfile.js', 'gulpfile.js', 'source/**']
        yarn {
            'jquery'('2.2.2')
            'bootstrap'('3.3.6')
        }
    }

    def commonWarDefs = {
        from 'res'
        from ('src/assets/vendor') {
            into 'client'
        }
        from 'build/favicons'
    }

    war {
        classifier = 'production'
        configure commonWarDefs
    }

    [war].each { task ->
        task.dependsOn clientInstall
        task.dependsOn generateFavicons
        task.from ('logging/' + task.classifier) {
            into('WEB-INF/classes')
            include "**/*"
        }
        task.from ('config/' + task.classifier) {
            into('WEB-INF/classes')
            include "**/*"
        }
    }
}
